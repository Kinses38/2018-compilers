/*******************************
 ***** SECTION 1 - OPTIONS *****
 *******************************/
options 
{ 
	JAVA_UNICODE_ESCAPE = true;
    IGNORE_CASE = true;
    DEBUG_TOKEN_MANAGER= false;
    DEBUG_PARSER = false;
    DEBUG_LOOKAHEAD = false;
	VISITOR = true;
	MULTI = true;
	NODE_PREFIX = "";
 }

/*********************************
 ***** SECTION 2 - USER CODE *****
 *********************************/
PARSER_BEGIN(CalParser)
import java.io.*;
import java.util.*;

public class CalParser {
    static String scope;
    static SymbolTable st;
	public static void main(String args[]){
		CalParser parser;
		if (args.length == 0){
			System.out.println("CAL parser: Reading from input: ");
			parser = new CalParser(System.in);
		} else if (args.length == 1) {
			System.out.println("CAL Parser: Reading from file " + args[0] + "..");
		
		try {
			parser = new CalParser(new FileInputStream(args[0]));
		} catch (java.io.FileNotFoundException e) {
			System.out.println("CAL Parser: File " + args[0] + "not found.");
			return;
			}
		}
		else {
			System.out.println("CAL Parser: Usage is one of:");
			System.out.println("		java CalParser < inputfile");
			System.out.println("OR");
			System.out.println("		java CalParser inputfile");
			return;
		}
		
		try {
			st = new SymbolTable();
			SimpleNode root = parser.Prog();

			System.out.println("\n### AST ###");
			root.dump("> ");
			System.out.println("\n### END AST ###");


            st.printSymbolTable();

            MyVisitor v = new MyVisitor();
            root.jjtAccept(v, st);

			System.out.println("CAL Parser: CAL program parsed successfully");
			}catch (ParseException e) {
				System.out.println(e.getMessage());
				System.out.println("CAL Parser: Encountered errors during parse.");

		}
	}
}
PARSER_END(CalParser)

/*****************************************
 ***** SECTION 3 - TOKEN DEFINITIONS *****
 *****************************************/

TOKEN_MGR_DECLS:
{
	static int commentNesting = 0;
}

SKIP : /*** Ignoring spaces/tabs/newlines ***/
{
	  " "
	| "\t"
	| "\n"
	| "\r"
	| "\f"
}

SKIP : /* Comments */
{
	  "/*" { commentNesting ++;} : IN_COMMENT
	| <"//" (~["\n"])* "\n">
}

<IN_COMMENT> SKIP : 
{
   "/*" { commentNesting++;}
   | "*/" { commentNesting--;
            if(commentNesting == 0)
                SwitchTo(DEFAULT);
            }
   |<~[]>
}

TOKEN : /*Keywords and Punctuation */
{
	<COMMA : ",">
	|<SEMIC : ";">
	|<COLON : ":">
	|<ASSIGN : ":=">
	|<LB : "(">
	|<RB : ")">
	|<PLUS : "+">
	|<MINUS : "-">
	|<NOT : "~">
	|<OR : "|">
	|<AND : "&">
	|<EQUAL : "=">
	|<NEQ : "!=">
	|<LT : "<">
	|<LTE : "<=">
	|<GT : ">">
	|<GTE : ">=">

	|<VAR : "variable">
	|<CONS : "constant">
	|<RET : "return">
	|<INT : "integer">
	|<BOOL : "boolean">
	|<VOID : "void">
	|<MAIN : "main">
	|<IF : "if">
	|<ELSE : "else">
	|<TRUE : "true">
	|<FALSE : "false">
	|<WHILE : "while">
	|<BEGIN : "begin">
	|<END : "end">
	|<IS : "is">
	|<SSKIP : "skip">

	|< #LETTER: (["a"-"z", "A"-"Z"])>
	|< IDENTIFIER: <LETTER> (<LETTER> | <NUMBER> | "_")* >
	|< #DIGIT: ["0" - "9"]>
	|< NUMBER: ("-" ["1" - "9"] (<DIGIT>)*)
			   |( ["1" - "9"] (<DIGIT>)* ) 
			   | "0">	

    |<OTHER : ~[]>
}


/***********************************
***** SECTION 4 - THE GRAMMAR *****
***********************************/

SimpleNode Prog() : {}
{
    {scope = "global";}

	decl_list() 
	function_list() 
	main()
	{return jjtThis;}
}

void decl_list() #void : {}
{
	(decl() <SEMIC> decl_list()) | {} 
}

void decl() #void : {}
{
	var_decl() | const_decl()
}

// Creating Identifier prod rule to return info based on ExprLang.jjt by David Sinclair
void var_decl() #Var_dec : {Token t; String id; String type;}
{
	t = <VAR> id = identifier() <COLON> type = type() {st.put(id, type, "var",  scope);}
}

String identifier() #Identifier : {Token t;}
{
    t = <IDENTIFIER> {jjtThis.value = t.image; return t.image;}
}

void const_decl() #Const_dec : {Token t; String id; String type;}
{
	t = <CONS>  id = identifier() <COLON> type = type() <ASSIGN> expression()
    {
        jjtThis.value = t.image;
        st.put(id, type, "const", scope);
    }
}

void function_list() #void : {}
{
	function() function_list() | {}
}

void function() #Function : {String id; String type;}
{
	type = type() id = identifier()
	{
            //function dec is global, then let parameters fall under func scope.
            st.put(id, type, "function", scope);
            scope = id;
    }
	<LB> parameter_list()<RB> <IS>
	decl_list()
	<BEGIN>
	statement_block()
	//ret type = func header type? Returning only one?
	<RET> <LB> (expression() #Ret(1)|{} #Ret) <RB> <SEMIC>
	<END>
	{scope = "global";}
}

String type() #Type : {Token t;}
{
	(t = <INT> | t = <BOOL> | t =  <VOID>)
	{jjtThis.value = t.image; return t.image;}
}

// rewrite "more parameters" to stop nesting of param list node
void parameter_list() #void  : {}
{
        parameter()  (LOOKAHEAD(2)   <COMMA> parameter_list())?
    | {}
}

void parameter() #void() : {String id; String type;}
{
	(id = identifier() <COLON> type = type() #Parameter(2))
	{st.put(id, type, "parameter", scope);}

}

void main() #Main: {}
{
    {scope = "main";}
	<MAIN>
	<BEGIN>
	decl_list()
	statement_block()
	<END>
}

void statement_block() #void : {}
{
	statement() statement_block() | {}
}

void statement() #void: {Token t;}
{
	  identifier() statement_beta()
	| <BEGIN> statement_block() <END>
	| (t = <IF> condition() <BEGIN> statement_block() <END> {jjtThis.value = t.image;}#Ifstate(2))
	| (t = <ELSE> <BEGIN> statement_block() <END> {jjtThis.value = t.image;} #Els(1))
	| (t = <WHILE> condition() <BEGIN> statement_block() <END> {jjtThis.value = t.image;}#Whil(2))
	| <SSKIP> <SEMIC>
}

void statement_beta() #void : {Token t;}
{
	  (t = <ASSIGN> expression() <SEMIC> {jjtThis.value = t.image;} #Assign(2))
	| (t = <LB> arg_list() <RB> <SEMIC> {jjtThis.value = t.image;} #Func_call(2))
}

//Expression > Fragment > Expression.
void expression() #void : {}
{
	  fragment() (binary_arith_op())
	  | (<LB> expression() <RB> binary_arith_op())
}

//rewrite bin op to allow children
void binary_arith_op() #void : {Token t;}
{
	  ( (t = <PLUS> fragment() {jjtThis.value = t.image;} #Plus(2))
	  | (t = <MINUS> fragment() {jjtThis.value = t.image;} #Minus(2)))*
}

void fragment() #void : {Token t;}
{
	  (t = <MINUS> identifier() {jjtThis.value = t.image;} #Minus_identifier(1))
	| (t = <NUMBER> {jjtThis.value = t.image;} #Num)
	| (t = <TRUE> {jjtThis.value = t.image;} #Bool)
	| (t = <FALSE> {jjtThis.value = t.image;} #Bool)
	|  identifier() (<LB> arg_list() <RB>)?
}

//Rewrite condition to make nodes?
void condition() #void : {Token t;}
{
	 condition_beta()
	 (LOOKAHEAD(2)  (t = <AND> condition_beta() {jjtThis.value = t.image;} #And(2)
     |  t = <OR> condition_beta() {jjtThis.value = t.image;} #Or(2)))*

}

//have to rewrite cond_beta to capture 2nd expression as child
void condition_beta() #void : {Token t;}
{
	  (t = <NOT> condition() {jjtThis.value = t.image;} #Not(1))
	| LOOKAHEAD(3) <LB> condition() <RB>
	//| expression() comp_op() expression()
	| expression() comp_op()
}

void comp_op() #void : {}
{
	  <EQUAL> expression() #Eq(2)
	| <NEQ> expression()   #Neq(2)
	| <LT> expression()    #Lt(2)
	| <LTE> expression()   #Lte(2)
	| <GT> expression()    #Gt(2)
	| <GTE> expression()   #Gte(2)
}

void arg_list() #Arg_list : {}
{
	((identifier() (<COMMA> identifier())*))*
}

